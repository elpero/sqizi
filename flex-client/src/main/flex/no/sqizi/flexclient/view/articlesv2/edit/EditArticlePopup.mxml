<?xml version="1.0" ?>
<mx:TitleWindow title="Edit Article"
                xmlns:mx="http://www.adobe.com/2006/mxml"
                xmlns:wizard="no.sqizi.flexclient.view.articles.addarticlewizard.*"
                alpha="1"
                width="1200"
                height="650"
                showCloseButton="true"
                close="PopUpManager.removePopUp(this)"
                creationComplete="handleCreationComplete()" xmlns:edit="no.sqizi.flexclient.view.articlesv2.edit.*"
                xmlns:helper="no.sqizi.flexclient.view.helper.*">
    <mx:VBox width="100%"
             height="100%"
             horizontalAlign="center"
             verticalAlign="middle">
        <mx:ViewStack id="editArticleWizardViewStack" width="100%" height="100%" selectedIndex="0">
            <helper:PleaseWaitScreen/>
            <edit:RedefineArticleMainData article="{_article}"/>
            <edit:AddRemoveImages article="{_article}"/>
        </mx:ViewStack>
        <mx:HBox width="100%" horizontalAlign="right">
            <mx:Button id="prev" label="Previous" click="handlePreviousClick()" enabled="{_article != null}"/>
            <mx:Button id="next" label="Next" click="handleNextClick()" enabled="{_article != null}"/>
        </mx:HBox>

    </mx:VBox>
    <mx:Script><![CDATA[
        import mx.binding.utils.BindingUtils;
        import mx.managers.PopUpManager;

        import mx.utils.ObjectUtil;

        import no.sqizi.flexclient.domain.Article;
        import no.sqizi.flexclient.helper.WizardStep;
        import no.sqizi.flexclient.model.ArticleModel;
        import no.sqizi.flexclient.model.CompanyModel;

        import org.swizframework.Swiz;

        private function handlePreviousClick():void {
            if (editArticleWizardViewStack.selectedIndex > 0) {
                editArticleWizardViewStack.selectedIndex--;
            }

        }

        public static const READY_TO_MOVE_NEXT_MESSAGE:String = "ready";

        private function switchView(e:Event = null):void {
            if (editArticleWizardViewStack.selectedIndex < editArticleWizardViewStack.getChildren().length - 1) {
                editArticleWizardViewStack.selectedIndex++;
            } else {
                PopUpManager.removePopUp(this);
            }

        }

        private function handleArticleModelChange(value:* = null):void {

            this._article = Article(ObjectUtil.copy(articleModel.article));
            if (_article != null) {
                 editArticleWizardViewStack.selectedIndex = 1;
            } else {
                editArticleWizardViewStack.selectedIndex = 0;
            }

        }

        private function handleCreationComplete():void {
            if (articleModel.article != null) {
                _article = Article(ObjectUtil.copy(articleModel.article));
            }
            Swiz.addEventListener(READY_TO_MOVE_NEXT_MESSAGE, switchView);
            BindingUtils.bindSetter(handleArticleModelChange, articleModel, "article");
        }

        private function handleNextClick():void {
            (editArticleWizardViewStack.selectedChild as WizardStep).moveToTheNextView();
        }


        [Autowire(bean="companyModel")]
        [Bindable]
        public var companyModel:CompanyModel;

        [Bindable]
        private var _article:Article;


        [Autowire(bean="articleModel")]
        [Bindable]
        public var articleModel:ArticleModel;

        ]]></mx:Script>
</mx:TitleWindow>